define URL = "https://api.github.com/repos/EricssonResearch/calvin-base/commits"
define DELAY = 60
define NO_CHANGE = 304

component LatestCommitDate() body -> date {
  """
  Extract the latest commit date from github server response body.

  The body may be empty [] on 304 responses

  Inputs:
    body : github server response body
  Outputs:
    date : date of most recent commit
  """

  getdate  : json.GetValue()
  compare  : std.Compare(op="!=")
  sampler  : std.SampleHold(default="")
  exc_handler : exception.ExceptionHandler(replace=true, replacement="")

  .body > getdate.container
  # What we get is a list of commits, each with nested dicts.
  # Pick the first commit and drill down to its date
  [0, "commit", "author", "date"] > getdate.key
  getdate.value > exc_handler.token
  exc_handler.token > compare.a
  exc_handler.status > voidport
  "" > compare.b
  compare.result > sampler.sample
  exc_handler.token > sampler.in
  sampler.out > .date
}

component ExtractETag() header -> tag {
  """
  Retrieve the latest ETag from github server response header.

  Inputs:
    header : github response header
  Outputs:
    tag : header 'ETag' value
  """

  gettag : json.GetValue()

  .header > gettag.container
  "etag" > gettag.key
  gettag.value > .tag
}

component ExtractCommit() dict -> commit_info {
  """
  Produce nicely a formatted string from a commit.

  Extracts date, committer, and commit message from a github commit.

  Inputs:
    dict : a github commit
  Outputs:
    commit_info : formatted string
  """

  get_commit : json.GetValue()
  fmt: text.Format(fmt="{author.date} {author.name} '{message}'")

  .dict > get_commit.container
  "commit" > get_commit.key
  get_commit.value > fmt.dict
  fmt.text > .commit_info
}

component Timer(url, delay) -> url {
  """
  Produces url argument at its url outport every delay seconds
  """
  init_url : flow.Init(data=url)
  delay : std.ClassicDelay(delay=delay)

  delay.token > init_url.in
  init_url.out > delay.token, .url

  # Debugging
  # log : io.Print()
  # init_url.out >log.token
}

component HeaderGen() in -> out {
  """
  Documentation please
  """
  init_header : flow.Init(data={})
  etag : ExtractETag()
  header : json.Dict(n=1)

  .in > etag.header
  etag.tag > header.value
  "If-None-Match" > header.key

  header.dict > init_header.in
  init_header.out > .out

}

component PackLatestCommitDate() in -> out {
  """
  Documentation please
  """
  commit_date : LatestCommitDate()
  params : json.SetValue()

  .in > commit_date.body
  {"sha":"develop"} > params.container
  commit_date.date > params.value
  "since" > params.key
  params.container > .out
}

component PrintCommits() commits ->  {
  """
  Documentation please
  """
  commitlist : json.Items()
  extract : ExtractCommit()
  printout : io.Print()

  .commits > commitlist.list
  commitlist.item > extract.dict
  extract.commit_info > printout.token
}

component ParamGen() status, data -> params {
  """
  Documentation please
  """
  cmp : std.Compare(op="=")
  route : flow.Switch()
  init_params : flow.Init(data={"sha":"develop", "per_page":1})
  pack : PackLatestCommitDate()

  .status > cmp.a
  NO_CHANGE > cmp.b
  cmp.result > route.switch
  .data > route.a
  init_params.out > route.b, .params
  route.a > pack.in
  pack.out > init_params.in
  route.b > voidport
}

component GetCommitData() url, params -> status, data {
  """
  Documentation please
  """
  header_gen : HeaderGen()
  request : net.HTTPGet()
  body_to_json : json.FromString()

  .url > request.URL
  .params > request.params
  header_gen.out > request.header

  request.data > body_to_json.string
  request.header > header_gen.in
  request.status > .status
  body_to_json.data > .data

}

timer : Timer(url=URL, delay=DELAY)
get_commit: GetCommitData()
param_gen : ParamGen()
print_commits: PrintCommits()

timer.url > get_commit.url
param_gen.params > get_commit.params

get_commit.data > print_commits.commits
get_commit.data > param_gen.data
get_commit.status > param_gen.status


